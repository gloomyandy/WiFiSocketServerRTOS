set(CMAKE_CXX_STANDARD 17)

set(srcs "Misc.cpp"
         "Listener.cpp"
         "SocketServer.cpp"
         "Connection.cpp"
         "DNSServer.cpp"
         "WirelessConfigurationMgr.cpp")
set(include_dirs ".")

if(IDF_TARGET STREQUAL "esp8266")
     list(APPEND srcs "esp8266/gpio.c" "esp8266/HSPI.cpp")
     list(APPEND include_dirs "esp8266")
elseif(IDF_TARGET STREQUAL "esp32c3")
     list(APPEND srcs "esp32c3/HSPI.cpp")
     list(APPEND include_dirs "esp32c3")
endif()

idf_component_register(SRCS "${srcs}"
                       INCLUDE_DIRS "${include_dirs}"
                       REQUIRES spi_flash mdns
                       PRIV_REQUIRES indicator nvs_flash wpa_supplicant)

idf_build_get_property(python PYTHON)
idf_build_get_property(build_dir BUILD_DIR)

add_custom_target(image ALL DEPENDS app bootloader
     COMMAND ${python} "${CMAKE_CURRENT_LIST_DIR}/create_image.py"
                    "cmake" "${build_dir}" "${build_dir}/DuetWiFiServer.bin")

if(IDF_TARGET STREQUAL "esp8266")
     target_compile_definitions(${COMPONENT_LIB} PRIVATE "ESP8266")
elseif(IDF_TARGET STREQUAL "esp32c3")
     target_compile_definitions(${COMPONENT_LIB} PRIVATE "ESP32C3")
endif()